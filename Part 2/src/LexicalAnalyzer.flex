
import java.util.regex.PatternSyntaxException;

/**
 *
 * Scanner class, generated by JFlex.
 * Function nextSymbol is the important one as it reads the file and returns the next matched toke.
 *
 */

%%// Options of the scanner

%class LexicalAnalyzer // Name
%unicode               // Use unicode
%line                  // Use line counter (yyline variable)
%column                // Use character counter by line (yycolumn variable)
%function nextSymbol
%type Symbol
%yylexthrow PatternSyntaxException

%eofval{
	return new Symbol(Terminal.EOS, yyline, yycolumn);
%eofval}

//Extended Regular Expressions

AlphaUpperCase    = [A-Z]
AlphaLowerCase    = [a-z]
Alpha             = {AlphaUpperCase}|{AlphaLowerCase}
Numeric           = [0-9]
AlphaNumeric      = {Alpha}|{Numeric}
// LowerAlphaNumeric = {AlphaLowerCase}|{Numeric}

BadInteger     = (0[0-9]+)
Integer        = ([1-9][0-9]*)|0
VarName        = ({AlphaLowerCase})({AlphaNumeric})*
LineFeed       = "\n"
CarriageReturn = "\r"
EndLine        = ({LineFeed}{CarriageReturn}?) | ({CarriageReturn}{LineFeed}?)
Space          = (\t | \f | " ")
Spaces         = {Space}+
Separator      = ({Spaces}) | ({EndLine}) // Was Space (no 's') was it a typo? (did produce a warning)
Any            = ([^"\n""\r"])*
UpToEnd        = ({Any}{EndLine}) | ({EndLine})

//Declare exclusive states
%xstate YYINITIAL, SHORTCOMMENTS, LONGCOMMENTS

%%// Identification of Symbols


<LONGCOMMENTS> {
// End of comment
	"''"			{yybegin(YYINITIAL);} // go back to analysis
  <<EOF>>          {throw new PatternSyntaxException("A comment is never closed.",yytext(),yyline);}
	[^]					     {} //ignore any character
}

<YYINITIAL> {
// Comments
    "''"              {yybegin(LONGCOMMENTS);} // go to ignore mode
    "**"{UpToEnd}     {} // go to ignore mode
// Code delimiters
  "begin"             {return new Symbol(Terminal.BEGIN, yyline, yycolumn, yytext());}
  "end"               {return new Symbol(Terminal.END, yyline, yycolumn, yytext());}
  "..."               {return new Symbol(Terminal.DOTS, yyline, yycolumn, yytext());}
// Assignation
  ":="                {return new Symbol(Terminal.ASSIGN, yyline, yycolumn, yytext());}
// Parenthesis
  "("                 {return new Symbol(Terminal.LPAREN, yyline, yycolumn, yytext());}
  ")"                 {return new Symbol(Terminal.RPAREN, yyline, yycolumn, yytext());}
// Brackets
  "{"                 {return new Symbol(Terminal.LBRACK, yyline, yycolumn, yytext());}
  "}"                 {return new Symbol(Terminal.RBRACK, yyline, yycolumn, yytext());}
// Arithmetic signs
  "+"                 {return new Symbol(Terminal.PLUS, yyline, yycolumn, yytext());}
  "-"                 {return new Symbol(Terminal.MINUS, yyline, yycolumn, yytext());}
  "*"                 {return new Symbol(Terminal.TIMES, yyline, yycolumn, yytext());}
  "/"                 {return new Symbol(Terminal.DIVIDE, yyline, yycolumn, yytext());}
// Logical operators
  "and"               {return new Symbol(Terminal.AND, yyline, yycolumn, yytext());}
  "or"                {return new Symbol(Terminal.OR, yyline, yycolumn, yytext());}
// Conditional keywords
  "if"                {return new Symbol(Terminal.IF, yyline, yycolumn, yytext());}
  "then"              {return new Symbol(Terminal.THEN, yyline, yycolumn, yytext());}
  "else"              {return new Symbol(Terminal.ELSE, yyline, yycolumn, yytext());}
// Loop keywords
  "while"             {return new Symbol(Terminal.WHILE, yyline, yycolumn, yytext());}
  "do"                {return new Symbol(Terminal.DO, yyline, yycolumn, yytext());}
// Comparison operators
  "="                 {return new Symbol(Terminal.EQUAL, yyline, yycolumn, yytext());}
  "<"                 {return new Symbol(Terminal.SMALLER, yyline, yycolumn, yytext());}
// IO keywords
  "print"             {return new Symbol(Terminal.PRINT, yyline, yycolumn, yytext());}
  "read"              {return new Symbol(Terminal.READ, yyline, yycolumn, yytext());}
// Numbers
  {BadInteger}        {System.err.println("Warning! Numbers with leading zeros are deprecated: " + yytext()); return new Symbol(Terminal.NUMBER, yyline, yycolumn, Integer.valueOf(yytext()));}
  {Integer}           {return new Symbol(Terminal.NUMBER, yyline, yycolumn, Integer.valueOf(yytext()));}
  {VarName}           {return new Symbol(Terminal.VARNAME,yyline, yycolumn,yytext());}
  {Separator}         {}// ignore spaces
  [^]                 {throw new PatternSyntaxException("Unmatched Symbol, out of Symbols",yytext(),yyline);} // unmatched Symbol gives an error
}
