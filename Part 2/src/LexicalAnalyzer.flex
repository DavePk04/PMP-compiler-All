
import java.util.regex.PatternSyntaxException;

/**
 *
 * Scanner class, generated by JFlex.
 * Function nextToken is the important one as it reads the file and returns the next matched toke.
 *
 */

%%// Options of the scanner

%class LexicalAnalyzer // Name
%unicode               // Use unicode
%line                  // Use line counter (yyline variable)
%column                // Use character counter by line (yycolumn variable)
%function nextToken
%type Token
%yylexthrow PatternSyntaxException

%eofval{
	return new Token(LexicalUnit.EOS, yyline, yycolumn);
%eofval}

//Extended Regular Expressions

AlphaUpperCase    = [A-Z]
AlphaLowerCase    = [a-z]
Alpha             = {AlphaUpperCase}|{AlphaLowerCase}
Numeric           = [0-9]
AlphaNumeric      = {Alpha}|{Numeric}
// LowerAlphaNumeric = {AlphaLowerCase}|{Numeric}

BadInteger     = (0[0-9]+)
Integer        = ([1-9][0-9]*)|0
VarName        = ({AlphaLowerCase})({AlphaNumeric})*
LineFeed       = "\n"
CarriageReturn = "\r"
EndLine        = ({LineFeed}{CarriageReturn}?) | ({CarriageReturn}{LineFeed}?)
Space          = (\t | \f | " ")
Spaces         = {Space}+
Separator      = ({Spaces}) | ({EndLine}) // Was Space (no 's') was it a typo? (did produce a warning)
Any            = ([^"\n""\r"])*
UpToEnd        = ({Any}{EndLine}) | ({EndLine})

//Declare exclusive states
%xstate YYINITIAL, SHORTCOMMENTS, LONGCOMMENTS

%%// Identification of tokens


<LONGCOMMENTS> {
// End of comment
	"''"			{yybegin(YYINITIAL);} // go back to analysis
  <<EOF>>          {throw new PatternSyntaxException("A comment is never closed.",yytext(),yyline);}
	[^]					     {} //ignore any character
}

<YYINITIAL> {
// Comments
    "''"              {yybegin(LONGCOMMENTS);} // go to ignore mode
    "**"{UpToEnd}     {} // go to ignore mode
// Code delimiters
  "begin"             {return new Token(LexicalUnit.BEG, yyline, yycolumn, yytext());}
  "end"               {return new Token(LexicalUnit.END, yyline, yycolumn, yytext());}
  "..."               {return new Token(LexicalUnit.DOTS, yyline, yycolumn, yytext());}
// Assignation
  ":="                {return new Token(LexicalUnit.ASSIGN, yyline, yycolumn, yytext());}
// Parenthesis
  "("                 {return new Token(LexicalUnit.LPAREN, yyline, yycolumn, yytext());}
  ")"                 {return new Token(LexicalUnit.RPAREN, yyline, yycolumn, yytext());}
// Brackets
  "{"                 {return new Token(LexicalUnit.LBRACK, yyline, yycolumn, yytext());}
  "}"                 {return new Token(LexicalUnit.RBRACK, yyline, yycolumn, yytext());}
// Arithmetic signs
  "+"                 {return new Token(LexicalUnit.PLUS, yyline, yycolumn, yytext());}
  "-"                 {return new Token(LexicalUnit.MINUS, yyline, yycolumn, yytext());}
  "*"                 {return new Token(LexicalUnit.TIMES, yyline, yycolumn, yytext());}
  "/"                 {return new Token(LexicalUnit.DIVIDE, yyline, yycolumn, yytext());}
// Logical operators
  "and"               {return new Token(LexicalUnit.AND, yyline, yycolumn, yytext());}
  "or"                {return new Token(LexicalUnit.OR, yyline, yycolumn, yytext());}
// Conditional keywords
  "if"                {return new Token(LexicalUnit.IF, yyline, yycolumn, yytext());}
  "then"              {return new Token(LexicalUnit.THEN, yyline, yycolumn, yytext());}
  "else"              {return new Token(LexicalUnit.ELSE, yyline, yycolumn, yytext());}
// Loop keywords
  "while"             {return new Token(LexicalUnit.WHILE, yyline, yycolumn, yytext());}
  "do"                {return new Token(LexicalUnit.DO, yyline, yycolumn, yytext());}
// Comparison operators
  "="                 {return new Token(LexicalUnit.EQUAL, yyline, yycolumn, yytext());}
  "<"                 {return new Token(LexicalUnit.SMALLER, yyline, yycolumn, yytext());}
// IO keywords
  "print"             {return new Token(LexicalUnit.PRINT, yyline, yycolumn, yytext());}
  "read"              {return new Token(LexicalUnit.READ, yyline, yycolumn, yytext());}
// Numbers
  {BadInteger}        {System.err.println("Warning! Numbers with leading zeros are deprecated: " + yytext()); return new Token(LexicalUnit.NUMBER, yyline, yycolumn, Integer.valueOf(yytext()));}
  {Integer}           {return new Token(LexicalUnit.NUMBER, yyline, yycolumn, Integer.valueOf(yytext()));}
  {VarName}           {return new Token(LexicalUnit.VARNAME,yyline, yycolumn,yytext());}
  {Separator}         {}// ignore spaces
  [^]                 {throw new PatternSyntaxException("Unmatched token, out of Tokens",yytext(),yyline);} // unmatched token gives an error
}
